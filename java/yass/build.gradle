apply plugin: 'java-library'
apply plugin: 'osgi'
apply plugin: 'maven'
apply plugin: 'signing'

group = "ch.softappeal.yass"

sourceSets {
    main { java { srcDir 'main' } }
    test { java { srcDir 'test' } }
}

dependencies {
    compileOnly websocket_dep
    testCompile jetty_dep
    testCompile undertow_dep
}

artifacts {
    archives(packageSources)
    archives(packageJavadoc)
}

task dist {
    doLast {
        pom {
            project {
                name project.name
                description 'Yet Another Service Solution'
                url 'https://github.com/softappeal/yass'
                licenses { license { name 'BSD-3-Clause' } }
                scm { url 'https://github.com/softappeal/yass' }
                organization { name 'softappeal GmbH Switzerland' }
                developers { developer { name 'Angelo Salvade' } }
            }
        }.writeTo("build/libs/${project.name}-${version}.pom")
        if (project.hasProperty('signing.password')) {
            def signPrefix = "build/libs/${project.name}-${version}"
            signing.sign(file("${signPrefix}.pom"))
            signing.sign(file("${signPrefix}.jar"))
            signing.sign(file("${signPrefix}-sources.jar"))
            signing.sign(file("${signPrefix}-javadoc.jar"))
        }
        ant.zip(destfile: "build/${project.name}-${version}_artifacts.zip") {
            fileset(dir: 'build/libs')
        }
    }
}
